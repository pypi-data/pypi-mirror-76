declare component tile_daoe_doublehoriz_35up_3h4h: eTL + eBR + eBM + eBL -> aTL + aBR + aBM + aBL
# bottom left tile is 3up, upper right tile is 5up
# bottom tile's bottom right sticky end is blunt; top tile's bottom right sticky end is hairpin
# just like Barish et al's Z56 for Variable-Width and Copy

## Sequences
# sticky ends
sequence eBL = "5N" : 5
sequence eTL = "5N" : 5
sequence eBR = "5N" : 5
sequence eBM = "5N" : 5
sequence aBL = "N" : 1
sequence aTL = "N" : 1
sequence aBR = "N" : 1
sequence aBM = "N" : 1

# core 
sequence sbTLarm = "7N" : 7
sequence sbBLarm = "7N" : 7 
sequence sbBRarm = "7N" : 7
sequence sbTcore = "N 14N N" : 16
sequence sbBcore = "N 14N N" : 16
sequence sMarm   = "21N" : 21 
sequence stTLarm = "8N" : 8
sequence stBRarm = "7N" : 7 
sequence stTRarm = "N 7N" : 8
sequence stTcore = "N 14N N" : 16
sequence stBcore = "N 14N N" : 16
sequence stTLhairpin_arm = "7N" : 7
sequence stTRhairpin_arm = "7N" : 7


## Strands
strand shortLeft    = eBL aBL sbBLarm sbTLarm* aTL eTL : 26
strand longLeft     = aTL* sbTLarm sbTcore sbBcore* sbBLarm* aBL* : 48
strand longMidLeft  = stTLhairpin_arm* "TTTT" stTLhairpin_arm stTLarm sMarm sbTcore* sbBcore sbBRarm* aBM* : 87
strand longMidRight = eBM aBM sbBRarm sMarm* stBcore stTcore* stTLarm* : 74
strand longRight    = aBR* stBRarm stBcore* stTcore stTRarm* : 48
strand shortRight   = stTRhairpin_arm* "TTTT" stTRhairpin_arm stTRarm stBRarm* aBR eBR : 39

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 26. + 48. + 87. + 74. + 48. + 39.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
