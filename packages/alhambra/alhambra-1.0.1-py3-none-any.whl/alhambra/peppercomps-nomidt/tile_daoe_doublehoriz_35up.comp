declare component tile_daoe_doublehoriz_35up: eTL + eTM + eTR + eBR + eBM + eBL -> aTL + aTM + aTR + aBR + aBM + aBL
# bottom left tile is 3up, upper right tile is 5up
# bottom tile's bottom right sticky end is blunt; top tile's bottom right sticky end is hairpin
# just like Barish et al's Z56 for Variable-Width and Copy

## Sequences
# sticky ends
sequence eBL = "5N" : 5
sequence eTL = "5N" : 5
sequence eTM = "5N" : 5
sequence eTR = "5N" : 5
sequence eBR = "5N" : 5
sequence eBM = "5N" : 5
sequence aBL = "N" : 1
sequence aTL = "N" : 1
sequence aTM = "N" : 1
sequence aTR = "N" : 1
sequence aBR = "N" : 1
sequence aBM = "N" : 1

# core 
sequence sbTLarm = "7N" : 7
sequence sbBLarm = "7N" : 7 
sequence sbBRarm = "7N" : 7
sequence sbTcore = "N 14N N" : 16
sequence sbBcore = "N 14N N" : 16
sequence sMarm   = "21N" : 21 
sequence stTLarm = "7N" : 7
sequence stBRarm = "7N" : 7 
sequence stTRarm = "7N" : 7
sequence stTcore = "N 14N N" : 16
sequence stBcore = "N 14N N" : 16


## Strands
strand shortLeft    = eBL aBL sbBLarm sbTLarm* aTL eTL : 26
strand longLeft     = aTL* sbTLarm sbTcore sbBcore* sbBLarm* aBL* : 48
strand longMidLeft  = eTM aTM stTLarm sMarm sbTcore* sbBcore sbBRarm* aBM* : 74
strand longMidRight = eBM aBM sbBRarm sMarm* stBcore stTcore* stTLarm* aTM* : 74
strand longRight    = aBR* stBRarm stBcore* stTcore stTRarm* aTR* : 48
strand shortRight   = eTR aTR stTRarm stBRarm* aBR eBR : 26

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 26. + 48. + 74. + 74. + 48. + 26.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
