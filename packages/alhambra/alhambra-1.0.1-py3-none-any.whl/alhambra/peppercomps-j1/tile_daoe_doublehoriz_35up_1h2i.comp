declare component tile_daoe_doublehoriz_35up_1h2i: eTR + eBR + eBM + eBL -> aTR + aBR + aBM + aBL

# bottom left tile is 3up, upper right tile is 5up
# bottom tile's upper-left sticky end is a hairpin; top tile's upper-left sticky end is inert
# just like Barish et al's Z78 for Copy (but not Variable-Width)

## Sequences
# sticky ends
sequence eBL = "5N" : 5
sequence eBM = "5N" : 5
sequence eBR = "5N" : 5
sequence eTR = "5N" : 5
sequence aBL = "N" : 1
sequence aBM = "N" : 1
sequence aBR = "N" : 1
sequence aTR = "N" : 1
sequence aTMinert = "N" : 1
sequence eTMinert = "5N" : 5

# core 
sequence sbTLhairpin_arm = "7N" : 7
sequence sbTLarm = "6N GG" : 8
sequence sbBLarm = "5N CA" : 7 
sequence sbBRarm = "5N GT" : 7
sequence sbTcore = "AC 12N CA" : 16
sequence sbBcore = "GG 5N AT 5N GG" : 16
sequence sMarm   = "CC 17N CC" : 21 
sequence stTLarm = "5N CA" : 7
sequence stBRarm = "5N CC" : 7 
sequence stTRarm = "5N GT" : 7
sequence stTcore = "GG 12N GG" : 16
sequence stBcore = "AC 5N TA 5N CA" : 16

## Strands
strand shortLeft    = eBL aBL sbBLarm sbTLarm* sbTLhairpin_arm "TTTT" sbTLhairpin_arm* : 39
strand longLeft     = sbTLarm sbTcore sbBcore* sbBLarm* aBL* : 48
strand longMidLeft  = eTMinert aTMinert stTLarm sMarm sbTcore* sbBcore sbBRarm* aBM* : 74
strand longMidRight = eBM aBM sbBRarm sMarm* stBcore stTcore* stTLarm* aTMinert* eTMinert* : 79
strand longRight    = aBR* stBRarm stBcore* stTcore stTRarm* aTR* : 48
strand shortRight   = eTR aTR stTRarm stBRarm* aBR eBR : 26

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 39. + 48. + 74. + 79. + 48. + 26.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
